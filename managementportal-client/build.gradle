import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

/*
 * Copyright (c) 2020. The Hyve
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 *
 * See the file LICENSE in the root of this repository.
 */
plugins {
    id 'org.jetbrains.kotlin.jvm' version "1.6.10"
    id 'org.jetbrains.dokka' version "1.6.10"
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

description = "Kotlin ManagementPortal client"

dependencies {
    api("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.10")
    implementation("org.jetbrains.kotlin:kotlin-reflect:1.6.10")
    api(project(":oauth-client-util"))

    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: okhttp_version

    implementation("com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version")
    runtimeOnly("com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jackson_version")

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junit_version
    testImplementation group: 'com.github.tomakehurst', name: 'wiremock', version: '2.27.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: mockito_version
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'

    testRuntimeOnly group: 'org.slf4j', name: 'slf4j-simple', version: slf4j_version
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junit_version
    testImplementation("com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version")
}

tasks.withType(KotlinCompile) {
    kotlinOptions {
        jvmTarget = "11"
        apiVersion = "1.6"
        languageVersion = "1.6"
    }
}

task ghPagesJavadoc(type: Copy, dependsOn: dokkaJavadoc) {
    from file("$buildDir/dokka/javadoc")
    into file("$rootDir/public/managementportal-client-javadoc")
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
    useJUnitPlatform()
}

ext.projectLanguage = "kotlin"
ext.publishToMavenCentral = true

apply from: "$rootDir/gradle/publishing.gradle"
