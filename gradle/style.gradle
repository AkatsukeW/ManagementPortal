apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'jacoco'

configurations {
    codacy
}

dependencies {
    codacy group: 'com.codacy', name: 'codacy-coverage-reporter', version: codacyVersion
}

checkstyle {
    toolVersion = '8.45.1'
    ignoreFailures = false
    maxWarnings = 0

    configFile = rootProject.file('config/checkstyle/checkstyle.xml')
}

pmd {
    toolVersion = '6.36.0'
    ignoreFailures = false

    consoleOutput = true

    ruleSets = []
    ruleSetFiles = rootProject.files("config/pmd/ruleset.xml")
}

pmdTest {
    ruleSetFiles = rootProject.files("config/pmd/test_ruleset.xml")
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
        html.required = true
    }
    dependsOn test
}

task sendCoverageToCodacy(type: JavaExec, dependsOn: jacocoTestReport) {
    mainClass.set('com.codacy.CodacyCoverageReporter')
    classpath = configurations.codacy
    args = ['report', '-l', 'Java', '-r', "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"]
}
