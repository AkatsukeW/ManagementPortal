import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.dsl.KotlinVersion
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.dokka'
}


sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

description = 'Library for Kotlin utility classes and functions'


dependencies {
    implementation group: 'org.slf4j', name: 'slf4j-api', version: slf4j_version
    api(platform("org.jetbrains.kotlinx:kotlinx-coroutines-bom:$coroutines_version"))
    api("org.jetbrains.kotlinx:kotlinx-coroutines-core")

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junit_version
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'

    testRuntimeOnly group: 'ch.qos.logback', name: 'logback-classic', version: logback_version
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junit_version
}

tasks.withType(KotlinCompile).configureEach {
    compilerOptions {
        jvmTarget = JvmTarget.JVM_11
        apiVersion = KotlinVersion.KOTLIN_1_8
        languageVersion = KotlinVersion.KOTLIN_1_8
    }
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
    useJUnitPlatform()
}

tasks.register('ghPagesJavadoc', Copy) {
    from file("$buildDir/dokka/javadoc")
    into file("$rootDir/public/radar-auth-javadoc")
    dependsOn(dokkaJavadoc)
}

ext.projectLanguage = "kotlin"

apply from: "$rootDir/gradle/style.gradle"
apply from: "$rootDir/gradle/publishing.gradle"
